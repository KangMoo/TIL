## 기본 문법



기본 수학

```
Io> 1 + 1
2

Io> 2 sqrt
==> 1.4142135623730951
```



변수

```io
Io> a := 1
==> 1

Io> a
==> 1

Io> b := 2 * 3
==> 6

Io> a + b
==> 7
```



조건

```io
Io> a := 2

Io> (a == 1) ifTrue("a is one" println) ifFalse("a is not one" println)
==> a is not one

Io> if(a == 1, writeln("a is one"), writeln("a is not one"))
==> a is not one

```





리스트

```io
Io> d := List clone append(30, 10, 5, 20)    
==> list(30, 10, 5, 20)

Io> d size
==> 4

Io> d print
==> list(30, 10, 5, 20)

Io> d := d sort
==> list(5, 10, 20, 30)

Io> d first
==> 5

Io> d last
==> 30

Io> d at(2)
==> 20

Io> d remove(30)
==> list(5, 10, 20)

Io> d atPut(1, 123)
==> list(5, 123, 20)

Io> list(30, 10, 5, 20) select(>10)
==> list(30, 20)

Io> list(30, 10, 5, 20) detect(>10)
==> 30

Io> list(30, 10, 5, 20) map(*2)
==> list(60, 20, 10, 40)

Io> list(30, 10, 5, 20) map(v, v*2)
==> list(60, 20, 10, 40)
```



반복문

```io
Io> for(i, 1, 10, write(i, " "))
==> 1 2 3 4 5 6 7 8 9 10 

Io> d foreach(i, v, writeln(i, ": ", v))
==> 0: 5
1: 123
2: 20

Io> list("abc", "def", "ghi") foreach(println)
==> abc
def
ghi
```



딕셔너리

```io
Io> dict := Map clone    
Io> dict atPut("hello", "a greeting")   
Io> dict atPut("goodbye", "a parting")   
Io> dict hasKey("hello")   
==> true

Io> dict hasValue("a greeting")   
==> true

Io> dict at("hello")   
==> a greeting

Io> dict keys  
==> list("hello", "goodbye")

Io> dict foreach(k, v, (k .. ":" .. v) println)
hello: a greeting
==> goodbye: a parting
```





객체

```
Io> Contact := Object clone
==>  Contact_0x7fbc3bc8a6d0:
  type = "Contact"

Io> Contact name ::= nil
==> nil

Io> Contact address ::= nil
==> nil

Io> Contact city ::= nil
==> nil

Io> holmes := Contact clone setName("Holmes") setAddress("221B Baker St") setCity("London")
==>  Contact_0x7fbc3be2b470:
  address          = "221B Baker St"
  city             = "London"
  name             = "Holmes"

Io> Contact fullAddress := method(list(name, address, city) join("\n"))
==> method(
    list(name, address, city) join("\n")
)

Io> holmes fullAddress
==> Holmes
221B Baker St
London
```